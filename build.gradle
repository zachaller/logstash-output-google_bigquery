import java.nio.file.Files

import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
}


dependencies {
    // Core dependency for BigQuery clients
    implementation "com.google.cloud:google-cloud-bigquery:2.4.1"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.13.0'
    implementation group: 'com.google.api', name: 'api-common', version: '2.1.0'
    implementation group: 'com.google.api', name: 'gax-httpjson', version: '0.92.0'
    implementation group: 'com.google.api', name: 'gax', version: '2.7.0'
    implementation group: 'com.google.api-client', name: 'google-api-client', version: '1.32.2'
    implementation group: 'com.google.api.grpc', name: 'proto-google-common-protos', version: '2.6.0'
    implementation group: 'com.google.api.grpc', name: 'proto-google-iam-v1', version: '1.1.7'
    implementation group: 'com.google.apis', name: 'google-api-services-bigquery', version: 'v2-rev20211106-1.32.1'
    implementation group: 'com.google.auth', name: 'google-auth-library-credentials', version: '1.3.0'
    implementation group: 'com.google.auth', name: 'google-auth-library-oauth2-http', version: '1.3.0'
    implementation group: 'com.google.cloud', name: 'google-cloud-core', version: '2.3.0'
    implementation group: 'com.google.cloud', name: 'google-cloud-core-http', version: '2.3.0'
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.9'
    implementation group: 'com.google.errorprone', name: 'error_prone_annotations', version: '2.10.0'
    implementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
    implementation group: 'com.google.guava', name: 'failureaccess', version: '1.0.1'
    implementation group: 'com.google.guava', name: 'listenablefuture', version: '9999.0-empty-to-avoid-conflict-with-guava'
    implementation group: 'com.google.http-client', name: 'google-http-client-gson', version: '1.40.1'
    implementation group: 'com.google.http-client', name: 'google-http-client-apache-v2', version: '1.40.1'
    implementation group: 'com.google.http-client', name: 'google-http-client-appengine', version: '1.40.1'
    implementation group: 'com.google.http-client', name: 'google-http-client-jackson2', version: '1.40.1'
    implementation group: 'com.google.http-client', name: 'google-http-client', version: '1.40.1'
    implementation group: 'com.google.j2objc', name: 'j2objc-annotations', version: '1.3'
    implementation group: 'com.google.oauth-client', name: 'google-oauth-client', version: '1.32.1'
    implementation group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.19.1'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.19.1'
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    implementation group: 'commons-logging', name: 'commons-logging', version: '1.2'
    implementation group: 'io.grpc', name: 'grpc-context', version: '1.42.1'
    implementation group: 'io.opencensus', name: 'opencensus-api', version: '0.28.0'
    implementation group: 'io.opencensus', name: 'opencensus-contrib-http-util', version: '0.28.0'
    implementation group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.14'
    implementation group: 'org.checkerframework', name: 'checker-compat-qual', version: '2.5.5'
    implementation group: 'org.checkerframework', name: 'checker-qual', version: '3.19.0'
    implementation group: 'org.threeten', name: 'threetenbp', version: '1.5.2'


}

task generateGemJarRequiresFile {
    doLast {
        File jars_file = file('lib/logstash-output-google_bigquery_jars.rb')
        jars_file.newWriter().withWriter { w ->
            w << "# AUTOGENERATED BY THE GRADLE SCRIPT. DO NOT EDIT.\n\n"
            w << "require \'jar_dependencies\'\n"
            configurations.runtimeClasspath.allDependencies.each {
                w << "require_jar(\'${it.group}\', \'${it.name}\', \'${it.version}\')\n"
            }
        }
    }
}

task vendor {
    doLast {
        String vendorPathPrefix = "vendor/jar-dependencies"
        configurations.runtimeClasspath.allDependencies.each { dep ->
            File f = configurations.runtimeClasspath.filter { it.absolutePath.contains("${dep.group}/${dep.name}/${dep.version}") }.singleFile
            String groupPath = dep.group.replaceAll('\\.', '/')
            File newJarFile = file("${vendorPathPrefix}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.jar")
            newJarFile.mkdirs()
            Files.copy(f.toPath(), newJarFile.toPath(), REPLACE_EXISTING)
        }
    }
}

vendor.dependsOn(generateGemJarRequiresFile)
